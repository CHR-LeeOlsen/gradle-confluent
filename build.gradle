// added because the github-release plugin used implementation instead of api/compile for plugin dependencies
// An issue exists for this, so hopefully it will be corrected soon
buildscript {
   dependencies {
      classpath 'com.squareup.okhttp3:okhttp:3.12.0'
      classpath 'com.j256.simplemagic:simplemagic:1.10'
      classpath 'org.zeroturnaround:zt-exec:1.10'
   }
}

plugins {
   id 'groovy'
   id 'java-gradle-plugin'
   id "com.gradle.plugin-publish" version "0.10.0"
   id "pl.allegro.tech.build.axion-release" version "1.10.0"
   id "com.github.breadmoirai.github-release" version "2.2.3"
   id 'org.unbroken-dome.test-sets' version '2.0.3'
   id "com.github.ben-manes.versions" version "0.20.0"
   id "com.redpillanalytics.gradle-analytics" version "1.1.19"
}

// send analytics
analytics {
   organization = 'Red Pill Analytics'
   sinks {
      pubsub
   }
}

scmVersion {

   tag {
      prefix = 'v'
      versionSeparator = ''
   }
   ignoreUncommittedChanges = false
}

//set Gradle version to SCM Version
allprojects {
   project.version = scmVersion.version
}

githubRelease {

   token = githubToken
   owner = 'RedPillAnalytics'
   repo = 'gradle-confluent'
   tagName = "v${version}"
   releaseAssets = fileTree(dir: buildDir, includes: ["**/*${version}*", "**/*${version}*.jar"]).toList()
}

dependencies {

   compile gradleApi()
   compile localGroovy()

   compile group: 'org.slf4j', name: 'slf4j-simple', version: '+'
   compile 'com.mashape.unirest:unirest-java:+'

   // Gradle Analytics plugin
   // It also has the Common library in it, thus the dependency here
   compile "gradle.plugin.com.redpillanalytics:gradle-analytics:+"

   testCompile 'org.spockframework:spock-core:1.2-groovy-2.5'
//   testCompile 'org.testcontainers:kafka:1.9.1'
//   testCompile 'org.testcontainers:spock:1.9.1'
}

// Default artifact naming.
group = 'com.redpillanalytics'

repositories {
   jcenter()
   maven {
      url "https://plugins.gradle.org/m2/"
   }
}

gradlePlugin {
   plugins {
      gradleConfluent {
         id = 'com.redpillanalytics.gradle-confluent'
         implementationClass = 'com.redpillanalytics.gradle.ConfluentPlugin'
      }
   }
}

pluginBundle {

   website = 'https://github.com/RedPillAnalytics/gradle-confluent'
   vcsUrl = 'https://github.com/RedPillAnalytics/gradle-confluent'

   plugins {

      gradleConfluent {
         id = 'com.redpillanalytics.gradle-confluent'
         displayName = 'gradle-confluent'
         description = "A plugin for deploying streaming applications to a Confluent Kafka cluster."
         tags = ['kafka', 'confluent', 'ksql', 'streams']
         version = project.version
      }
   }
}

// Options for all tests
tasks.withType(Test) {
   ignoreFailures true
   testLogging.showStandardStreams true
   systemProperty 'projectDir', temporaryDir
}

testSets {
   ksqlServerTest
   ksqlPipelinesTest
   deployTest
}

task('cleanJunit', type: Delete) {

   delete getTestResultsDir()
}

ext.bucket = 's3://documentation.redpillanalytics.com'
ext.bucketPath = 'gradle-confluent'


task publishDocs {

   description = 'Upload Groovydocs to s3.'
   group = 'documentation'

   doLast {

      exec {
         executable 'aws'
         args 's3', 'sync', groovydoc.destinationDir.getPath(), "${bucket}/${bucketPath}/${version}/"
      }

      exec {
         executable 'aws'
         args 's3', 'sync', groovydoc.destinationDir.getPath(), "${bucket}/${bucketPath}/latest/"
      }
   }

   dependsOn groovydoc
}

tasks.publishPlugins.dependsOn publishDocs

task runAllTests {
   description 'Run all defined tests.'
   group 'verification'
}

tasks.withType(Test) {
   runAllTests.dependsOn it
}