plugins {
   id 'groovy'
   id 'java-gradle-plugin'
   id "com.gradle.plugin-publish" version "0.10.0"
   id "pl.allegro.tech.build.axion-release" version "1.9.3"
   id "com.github.breadmoirai.github-release" version "2.0.1"
   id 'org.unbroken-dome.test-sets' version '1.5.1'
   id "com.github.ben-manes.versions" version "0.20.0"
   id "com.redpillanalytics.gradle-analytics" version "1.1.9"
}

// send analytics
analytics {
   organization = 'Red Pill Analytics'
   sinks {
      pubsub
   }
}

scmVersion {

   tag {
      prefix = 'v'
      versionSeparator = ''
   }
   ignoreUncommittedChanges = false
}

//set Gradle version to SCM Version
allprojects {
   project.version = scmVersion.version
}

githubRelease {

   token = githubToken
   owner = 'RedPillAnalytics'
   repo = 'gradle-confluent'
   tagName = "v${version}"
   releaseAssets = fileTree(dir: buildDir, includes: ["**/*${version}*", "**/*${version}*.jar"]).toList()
}

dependencies {

   compile gradleApi()
   compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.15'
   compile group: 'org.slf4j', name: 'slf4j-simple', version: '+'
   //compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7'
   compile 'com.github.groovy-wslite:groovy-wslite:1.1.2'
   compile 'com.google.code.gson:gson:2.8.5'

   testCompile('org.spockframework:spock-core:1.1-groovy-2.4')
}

// Default artifact naming.
group = 'com.redpillanalytics'

repositories {
   jcenter()
   maven {
      url "https://plugins.gradle.org/m2/"
   }
}

gradlePlugin {
   plugins {
      gradleConfluent {
         id = 'com.redpillanalytics.gradle-confluent'
         implementationClass = 'com.redpillanalytics.gradle.ConfluentPlugin'
      }
   }
}

pluginBundle {

   website = 'https://github.com/RedPillAnalytics/gradle-confluent'
   vcsUrl = 'https://github.com/RedPillAnalytics/gradle-confluent'

   plugins {

      gradleConfluent {
         id = 'com.redpillanalytics.gradle-confluent'
         displayName = 'gradle-confluent'
         description = "A plugin for deploying streaming applications to a Confluent Kafka cluster."
         tags = ['kafka', 'confluent', 'ksql', 'streams']
         version = project.version
      }
   }
}

// Options for all tests
tasks.withType(Test) {
   ignoreFailures true
   testLogging.showStandardStreams true
   systemProperty 'projectDir', temporaryDir
}

//release.onlyIf { gradle.rootProject.extensions.analytics.gitBranch == 'master' }
//publishPlugins.onlyIf { gradle.rootProject.extensions.analytics.gitBranch == 'master' }
//
//release.doLast {
//   logger.info "branch: ${gradle.rootProject.extensions.analytics.gitBranch}"
//}

testSets {
   ksqlServerTest
   deployTest
}

task('cleanJunit', type: Delete) {

   delete getTestResultsDir()
}

ext.bucket = 's3://documentation.redpillanalytics.com'
ext.bucketPath = 'gradle-confluent'


task publishDocs {

   description = 'Upload Groovydocs to s3.'
   group = 'documentation'

   doLast {

      exec {
         executable 'aws'
         args 's3', 'sync', groovydoc.destinationDir.getPath(), "${bucket}/${bucketPath}/${version}/"
      }

      exec {
         executable 'aws'
         args 's3', 'sync', groovydoc.destinationDir.getPath(), "${bucket}/${bucketPath}/current/"
      }
   }

   dependsOn groovydoc
}

tasks.publishPlugins.dependsOn publishDocs
