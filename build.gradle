plugins {
   id 'groovy'
   id 'java-gradle-plugin'
   id "com.gradle.plugin-publish" version "0.10.1"
   id "pl.allegro.tech.build.axion-release" version "1.10.2"
   id "com.github.breadmoirai.github-release" version "2.2.9"
   id 'org.unbroken-dome.test-sets' version '2.1.1'
   id "com.avast.gradle.docker-compose" version "0.9.4"
   id "com.github.ben-manes.versions" version "0.24.0"
   id "com.redpillanalytics.gradle-analytics" version "1.2.3"
   id 'com.adarshr.test-logger' version '1.7.0'
   id 'build-dashboard'
}

// send analytics
analytics {
   organization = 'Red Pill Analytics'
   sinks {
      pubsub
   }
}

scmVersion {

   tag {
      prefix = 'v'
      versionSeparator = ''
   }
   ignoreUncommittedChanges = true
}

//set Gradle version to SCM Version
allprojects {
   project.version = scmVersion.version
}

githubRelease {

   token = githubToken
   owner = 'RedPillAnalytics'
   repo = 'gradle-confluent'
   tagName = "v${version}"
   releaseAssets = fileTree(dir: buildDir, includes: ["**/*${version}*", "**/*${version}*.jar"]).toList()
}

dependencies {

   compile gradleApi()
   compile localGroovy()

   compile group: 'org.slf4j', name: 'slf4j-simple', version: '+'
   compile 'com.konghq:unirest-java:+'

   // Gradle Analytics plugin
   // It also has the Common library in it, thus the dependency here
   compile "gradle.plugin.com.redpillanalytics:gradle-analytics:+"

   // tsting
   testCompile 'org.spockframework:spock-core:1.2-groovy-2.5'
}

// Default artifact naming.
group = 'com.redpillanalytics'

repositories {
   jcenter()
   maven {
      url "https://plugins.gradle.org/m2/"
   }
}

gradlePlugin {
   plugins {
      gradleConfluent {
         id = 'com.redpillanalytics.gradle-confluent'
         implementationClass = 'com.redpillanalytics.gradle.ConfluentPlugin'
      }
   }
}

pluginBundle {

   website = 'https://github.com/RedPillAnalytics/gradle-confluent'
   vcsUrl = 'https://github.com/RedPillAnalytics/gradle-confluent'

   plugins {

      gradleConfluent {
         id = 'com.redpillanalytics.gradle-confluent'
         displayName = 'gradle-confluent'
         description = "A plugin for deploying streaming applications to a Confluent Kafka cluster."
         tags = ['kafka', 'confluent', 'ksql', 'streams']
         version = project.version
      }
   }
}

testSets {
   ksqlServerTest
   ksqlPipelinesTest
   deployTest
   buildTest
}


task cleanJunit(type: Delete) {
   delete getTestResultsDir()
}

task cleanLibs(type: Delete) {
   delete getLibsDir()
}

def reportUrl = "${actionsBucket}/${rootProject.name}/${executionId}"
task publishTestReports(type: Exec) {
   description = 'Copy Gradle test reports to S3.'
   group = 'verification'
   executable 'aws'
   args 's3', 'sync', reportsDir, "s3://${reportUrl}"
   doLast{
      logger.warn "\nTest reports: https://s3.amazonaws.com/${reportUrl}/buildDashboard/index.html"
   }
   dependsOn buildDashboard
}

def versionUrl = "${docBucket}/${rootProject.name}/${version}"
task publishVersionDocs(type: Exec) {
   description = 'Sync version Groovydocs to S3.'
   group = 'documentation'
   executable 'aws'
   args 's3', 'sync', groovydoc.destinationDir.getPath(), "s3://${versionUrl}"
   doLast{
      logger.warn "\nVersion docs: https://s3.amazonaws.com/${versionUrl}/index.html"
   }
   dependsOn groovydoc
}

def latestUrl = "${docBucket}/${rootProject.name}/latest"
task publishLatestDocs(type: Exec) {
   description = 'Sync version Groovydocs to S3.'
   group = 'documentation'
   executable 'aws'
   args 's3', 'sync', groovydoc.destinationDir.getPath(), "s3://${latestUrl}"
   doLast{
      logger.warn "\nVersion docs: https://s3.amazonaws.com/${latestUrl}/index.html"
   }
   dependsOn groovydoc
}

tasks.publishPlugins.dependsOn publishVersionDocs, publishLatestDocs, githubRelease

task runAllTests {
   description 'Run all defined tests.'
   group 'verification'
}

tasks.withType(Test) {
   runAllTests.dependsOn it
   composeDown.mustRunAfter it
   publishTestReports.mustRunAfter it
   mustRunAfter composeUp
   failFast false
   ignoreFailures false
   testLogging.showStandardStreams true
   systemProperty 'projectDir', temporaryDir
}
